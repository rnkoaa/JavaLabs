plugins {
    id 'java'
    id 'idea'
    id 'groovy'
//    id 'jacoco' // HERE!
}

java {
    sourceCompatibility = JavaVersion.VERSION_15
    targetCompatibility = JavaVersion.VERSION_15

    // has issues with preview-features
//    toolchain {
//        languageVersion = JavaLanguageVersion.of(15)
//    }
}


group 'com.marketplace.eventstoredb'
version '1.0.0-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

sourceSets {
  functional {
    groovy {
      srcDirs += "src/functional/groovy"
    }
    resources {
      srcDirs += file("src/functional/groovy")
    }
    compileClasspath += main.output + test.output
    runtimeClasspath += main.output + test.output
  }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    functionalImplementation.extendsFrom testImplementation
    functionalRuntime.extendsFrom testRuntime
}

idea {
  module {
    iml {
      generateTo = file("$projectDir/.project/")
    }
    sourceDirs -= file("src/functional/groovy")
    testSourceDirs += file("src/functional/groovy")
    resourceDirs -= file("src/functional/resources")
    testResourceDirs += file("src/functional/resources")
  }
}


dependencies {

    implementation("org.slf4j:slf4j-api:1.7.30")
    implementation("org.slf4j:slf4j-log4j12:1.7.30")

    compileOnly('org.projectlombok:lombok:1.18.16')
    annotationProcessor('org.projectlombok:lombok:1.18.16')

    // 2.11.3
    implementation("com.fasterxml.jackson.core:jackson-databind:$jacksonVersion")
    implementation("com.fasterxml.jackson.core:jackson-core:$jacksonVersion")
    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:$jacksonVersion")
    implementation("com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion")

    implementation("com.google.guava:guava:${guavaVersion}")

    testImplementation(platform("org.junit:junit-bom:${junitVersion}"))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation("org.assertj:assertj-core:${assertjVersion}")
    testImplementation("org.mockito:mockito-core:${mockitoVersion}")
    testImplementation("org.mockito:mockito-junit-jupiter:${mockitoVersion}")
    testImplementation("org.testcontainers:junit-jupiter:1.15.0")
    testImplementation("org.testcontainers:mongodb:1.15.0")

    testImplementation("org.codehaus.groovy:groovy-all:2.5.13")
    testImplementation("org.spockframework:spock-core:1.2-groovy-2.5"){
      exclude group: "org.codehaus.groovy", module: "groovy-all"
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs += "--enable-preview"
}
tasks.withType(Test) {
    jvmArgs += "--enable-preview"
}
tasks.withType(JavaExec) {
    jvmArgs += "--enable-preview"
}

task functionalTest(type: Test) {
  description = "run functional tests in eventstore db"
  testClassesDirs = sourceSets.functional.output.classesDirs
  classpath = sourceSets.functional.runtimeClasspath
  group = "verification"
}

// check.dependsOn functionalTest

